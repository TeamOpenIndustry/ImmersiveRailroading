buildscript {
    repositories {
        maven { url = "https://maven.minecraftforge.net/" }
    }
	dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
	}
}


apply plugin: 'net.minecraftforge.gradle.forge'

apply plugin: 'maven-publish'


version = '1.12.2-forge-1.9.1'
if (!"release".equalsIgnoreCase(System.getProperty("target"))) {
    try {
        version += "-" + 'git rev-parse --verify --short=7 HEAD'.execute().text.trim()
    } catch (Exception ex) {
        version += "-unknown"
    }
}
group = "cam72cam.immersiverailroading"
archivesBaseName = "ImmersiveRailroading"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "stable_39"
}


repositories { maven { url = "https://teamopenindustry.cc/maven" }}

dependencies {
    compile 'cam72cam.universalmodcore:UniversalModCore:1.12.2-forge-1.1.4-a8ed496'
}


apply from: 'ImmersiveRailroadingIntegration/dependencies.gradle'

jar.finalizedBy('reobfJar')

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "sftp://teamopenindustry.cc:22/var/www/html/maven/"
            credentials {
                username = "mavenci"
                password = System.getenv("MAVENCI_PASS")
            }
        }
    }
}

task ('showBuildInfo') {
    doLast {
        String minecraftString = "1.12.2"
        String minecraftLoader = "forge"
        String umcVersionString = "1.12.2-forge-#UMC#"
        System.out.println("Build for MC 1.12.2 (forge)")
        System.out.println("https://teamopenindustry.cc/maven/cam72cam/immersiverailroading/ImmersiveRailroading/${version}/ImmersiveRailroading-${version}.jar")
        System.out.println("https://teamopenindustry.cc/maven/cam72cam/universalmodcore/UniversalModCore/${umcVersionString}/UniversalModCore-${umcVersionString}.jar")
    }
}

publish.finalizedBy showBuildInfo
