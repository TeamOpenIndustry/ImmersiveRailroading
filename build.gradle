import cam72cam.universalmodcore.Util;

buildscript {
    repositories {
        jcenter()
        maven { url = 'https://teamopenindustry.cc/maven/'  }
    }
	dependencies {
        classpath 'cam72cam.universalmodcore:UniversalModCoreGradle:0.1.3'
	}
}

apply plugin: 'cam72cam.universalmodcore'
apply plugin: 'maven'

String irVersion = '1.9.0'
if (!"release".equalsIgnoreCase(System.getProperty("target"))) {
    irVersion += "-" + Util.GitRevision()
}

universalmodcore {
    modPackage = 'cam72cam.immersiverailroading'
    modClass = 'ImmersiveRailroading'
    modName = 'Immersive Railroading'
    modId = 'immersiverailroading'
    modVersion = irVersion
    umcVersion = '1.1.3'
    //umcPath = '../UMC-forge-1.12.2/build/libs/'
}

task ('umcIntegration') {
    doLast {
        String umcLoader = System.getProperty("umc.loader")
        Util.GitClone("git@github.com:TeamOpenIndustry/ImmersiveRailroadingIntegration.git", umcLoader, "ImmersiveRailroadingIntegration");
        apply from: 'ImmersiveRailroadingIntegration/setup.gradle'
    }
}

umc.finalizedBy umcIntegration

if ((new File('umc.gradle')).exists()) {
    apply from: 'umc.gradle'
    apply from: 'ImmersiveRailroadingIntegration/dependencies.gradle'
}

task deployJar(type: Jar)

configurations {
    deployerJars
}

dependencies {
    deployerJars "org.apache.maven.wagon:wagon-ssh:2.9"
}

uploadArchives {
    tasks.uploadArchives.dependsOn build
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        repository(url: "scp://teamopenindustry.cc/var/www/html/maven/") {
            authentication(userName: "mavenci", privateKey: "publish_key")
        }
    }
}

task ('showBuildInfo') {
    doLast {
        String minecraftString = version.split('-')[0]
        String minecraftLoader = version.split('-')[1]
        String umcVersionString = "${minecraftString}-${minecraftLoader}-${universalmodcore.umcVersion}"
        System.out.println("Build for MC ${minecraftString} (${minecraftLoader})")
        System.out.println("https://teamopenindustry.cc/maven/cam72cam/immersiverailroading/ImmersiveRailroading/${version}/ImmersiveRailroading-${version}.jar")
        System.out.println("https://teamopenindustry.cc/maven/cam72cam/universalmodcore/UniversalModCore/${umcVersionString}/UniversalModCore-${umcVersionString}.jar")
    }
}

uploadArchives.finalizedBy showBuildInfo
